import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.gradle.vanilla.repository.MinecraftPlatform

plugins {
    id "org.spongepowered.gradle.plugin" version "2.3.0"
    id "org.spongepowered.gradle.vanilla" version "0.2.2-SNAPSHOT"
}

// Shadow plugin applied even though shadow is not used in the module,
// so that VanillaGradle doesn't include Minecraft in runtimeClassPath
apply plugin: "com.gradleup.shadow"

dependencies {
    implementation project(":common")

    implementation "net.playeranalytics:platform-abstraction-layer-sponge:$palVersion"
    implementation "org.bstats:bstats-sponge:$bstatsVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    annotationProcessor "org.spongepowered:spongeapi:$spongeVersion"
    compileOnly "org.spongepowered:mixin:0.8.7"

    testImplementation "org.spongepowered:spongeapi:$spongeVersion"
    testImplementation(testFixtures(project(":common")))
    testImplementation(project(":extensions:adventure"))
}

sponge {
    apiVersion(spongeVersion)
    license("LGPLv3")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin("plan") {
        displayName("Plan")
        version(fullVersion)
        entrypoint("com.djrapitops.plan.PlanSponge")
        description("Player Analytics Plugin by AuroraLS3")
        contributor("AuroraLS3") {
            description("Author")
        }

        dependency("spongeapi") {
            loadOrder(org.spongepowered.plugin.metadata.model.PluginDependency.LoadOrder.AFTER)
            optional(false)
            version("$spongeVersion")
        }
        // Dependencies
        // version is a *required* property (empty string works fine)
        dependency("griefprevention") {
            optional(true)
            version("*")
        }
        dependency("luckperms") {
            optional(true)
            version("*")
        }
        dependency("nucleus") {
            optional(true)
            version("*")
        }
        dependency("redprotect") {
            optional(true)
            version("*")
        }
        dependency("nuvotifier") {
            optional(true)
            version("*")
        }
    }
}

minecraft {
    version("1.21.3")
    platform(MinecraftPlatform.SERVER)
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(21)
}
