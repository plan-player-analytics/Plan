import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.gradle.vanilla.repository.MinecraftPlatform

plugins {
    id "org.spongepowered.gradle.plugin" version "2.0.0"
    id "org.spongepowered.gradle.vanilla" version "0.2"
}

dependencies {
    compileOnly project(":common")
    implementation project(path: ":common", configuration: 'shadow')
    compileOnly project(":api")

    // TODO: remove -sponge from version once sponge api 8 is merged to the mainstream PAL
    implementation "net.playeranalytics:platform-abstraction-layer-sponge:$palVersion-sponge"
    implementation "org.bstats:bstats-sponge:$bstatsVersion"

    testImplementation "org.spongepowered:spongeapi:$spongeVersion"
    annotationProcessor "org.spongepowered:spongeapi:$spongeVersion"
    implementation "org.spongepowered:mixin:0.7.11-SNAPSHOT"

    testImplementation project(path: ":common", configuration: 'testArtifacts')
}

sponge {
    apiVersion(spongeVersion)
    license("LGPLv3")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin("plan") {
        displayName("Plan")
        version(fullVersion)
        entrypoint("com.djrapitops.plan.PlanSponge")
        description("Player Analytics Plugin by AuroraLS3")
        contributor("AuroraLS3") {
            description("Author")
        }

        // Dependencies
        // version is a *required* property (empty string works fine)
        dependency("griefprevention") {
            optional(true)
            version("")
        }
        dependency("luckperms") {
            optional(true)
            version("")
        }
        dependency("nucleus") {
            optional(true)
            version("")
        }
        dependency("redprotect") {
            optional(true)
            version("")
        }
        dependency("nuvotifier") {
            optional(true)
            version("")
        }
    }
}

minecraft {
    version("1.16.5")
    platform(MinecraftPlatform.JOINED)
}

compileJava {
    options.release = 11
}

shadowJar {
    relocate "org.bstats", "com.djrapitops.plan.utilities.metrics"
}
