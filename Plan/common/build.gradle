dependencies {
    implementation "net.playeranalytics:platform-abstraction-layer-api:$palVersion"
    implementation project(":api")
    compileOnly project(":extensions")
    implementation project(path: ":extensions", configuration: 'shadow')
    implementation "org.apache.commons:commons-text:$commonsTextVersion"
    implementation "org.apache.commons:commons-compress:$commonsCompressVersion"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
    implementation "mysql:mysql-connector-java:$mysqlVersion"
    implementation "org.xerial:sqlite-jdbc:$sqliteVersion"
    implementation "com.zaxxer:HikariCP:$hikariVersion"
    implementation "org.slf4j:slf4j-nop:$slf4jVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "com.maxmind.geoip2:geoip2:$geoIpVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    testImplementation project(":api")
    testImplementation "com.google.code.gson:gson:$gsonVersion"
}

import org.apache.tools.ant.filters.ReplaceTokens
task updateVersion(type: Copy) {
    from('src/main/resources') {
        include 'plugin.yml'
        include 'bungee.yml'
        include 'nukkit.yml'
    }
    into 'build/sources/resources/'
    filter(ReplaceTokens, tokens: [version: '' + project.ext.fullVersion])
}
processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    dependsOn updateVersion
    from 'build/sources/resources'
}

shadowJar {
    dependsOn processResources

    // Exclude these files
    exclude "**/*.svg"
    exclude "**/*.psd"

    exclude "**/module-info.class"
    exclude "module-info.class"
    exclude 'META-INF/versions/' // Causes Sponge to crash
    exclude 'org/apache/http/**/*' // Unnecessary http client depended on by geolite2 implementation
    exclude 'mozilla/**/*'

    // Exclude unnecessary SQLite drivers
    exclude '**/Linux/android-arm/libsqlitejdbc.so'
    exclude '**/DragonFlyBSD/**/libsqlitejdbc.so'
    exclude '**/sqlite/jdbc3/*'

    relocate 'com.google.protobuf', 'plan.com.mysql.cj.x.google.protobuf'

    relocate 'com.maxmind', 'plan.com.maxmind'
    relocate 'com.fasterxml', 'plan.com.fasterxml'
    relocate 'com.zaxxer', 'plan.com.zaxxer'
    relocate 'com.google.gson', 'plan.com.google.gson'
    relocate 'com.google.errorprone', 'plan.com.google.errorprone'
    relocate 'org.bstats', 'plan.org.bstats'
    relocate 'org.slf4j', 'plan.org.slf4j'

    // Exclude test dependencies
    exclude "org/junit/**/*"
    exclude "org/opentest4j/**/*"
    exclude "org/checkerframework/**/*"
    exclude "org/apiguardian/**/*"
    exclude "org/mockito/**/*"
    exclude "org/selenium/**/*"
    exclude "org/jayway/**/*"
    exclude "google/protobuf/**/*"
    exclude "jargs/gnu/**/*"
}